 1C_Excel_Reader
Это код для внешней обработки предназначенной для считывания работ по ремнту из Excel файла 
и записи их в соответствующую подсистему конфигурации 1С:Управление автотранспортом ПРОФ версии 2.2п.21

при этом формат данных в Excel файле должен выглядить следующим образом:
 номер по каталогу | наименование | количество нормочасов 
в самом коде комментарием указано место где вписываются (вручную!) автомобили,
для которых непосредственно задаются ремонтные работы



&НаКлиенте
Процедура ПрочитатьФайл(Команда)
	Проводник = Новый ПараметрыДиалогаПомещенияФайлов();
	Проводник.Заголовок = "Выберите файл"; 	
	Проводник.Фильтр = "Файл Excel (*.xlsx)|*.xlsx";;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗавершениеПрочитатьФайл_XLS", ЭтотОбъект);	
	НачатьПомещениеФайлаНаСервер(ОписаниеОповещенияОЗавершении,,,,Проводник, Новый УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПрочитатьФайл_XLS(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Расширение = ОписаниеПомещенногоФайла.СсылкаНаФайл.Расширение;
	ПрочитатьФайл_XLS_НаСервере(ОписаниеПомещенногоФайла.Адрес, Расширение);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФайл_XLS_НаСервере(АдресДанных, Расширение)
	Если НЕ ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось прочитать указанный файл по причине: неверный адрес временного хранилища";
		Сообщение.Сообщить();  
		Возврат;	
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;  
	
	Данные = ПолучитьИзВременногоХранилища(АдресДанных);
	ПутьКФайлуНаСервере = ПолучитьИмяВременногоФайла(Расширение);
	Данные.Записать(ПутьКФайлуНаСервере);
		
	Попытка
		ТабДок.Прочитать(ПутьКФайлуНаСервере);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось прочитать указанный файл по причине: " + ОписаниеОшибки();
		Сообщение.Сообщить();  
		Возврат;
	КонецПопытки;
	
	КоличествоСтрок = ТабДок.ВысотаТаблицы;
	Для НомерСтроки = 2 По КоличествоСтрок Цикл
		СтрокаДанных = Объект.ДанныеФайла.Добавить();
		СтрокаДанных.НомерПоКаталогу 	= ТабДок.ПолучитьОбласть(НомерСтроки, 1).ТекущаяОбласть.Текст;
		СтрокаДанных.Наименование 		= ТабДок.ПолучитьОбласть(НомерСтроки, 2).ТекущаяОбласть.Текст;
		СтрокаДанных.КоличествоНЧ 		= ТабДок.ПолучитьОбласть(НомерСтроки, 3).ТекущаяОбласть.Текст;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеНаСервере() 
	МассивАвто = Новый Массив;
	// В МАССИВ ДОБАВЛЯЕМ АВТОМОБИЛИ ДЛЯ КОТОРЫХ 
	// ЗАПИШЕМ РАБОТЫ ПО РЕМОНТУ  
	// 
	//МассивАвто.Добавить("Урал 32551-0013-41");
	//МассивАвто.Добавить("Урал 43202");
	//МассивАвто.Добавить("Урал 4784-01");
	//МассивАвто.Добавить("Урал 596012");
	//МассивАвто.Добавить("Урал КС 457117");
	//МассивАвто.Добавить("Урал КС 55713-3К-1");
	//МассивАвто.Добавить("Урал КС-45717-1");
	//МассивАвто.Добавить("Урал с КМУ");
	//МассивАвто.Добавить("Урал-4320-0110-31");	
	МассивСсылок = Новый Массив;
	Для каждого Элемент Из МассивАвто Цикл
		Ссылка = Справочники.уатМоделиТС.НайтиПоНаименованию(Элемент);  	
		Если Не Ссылка.Пустая() Тогда
			МассивСсылок.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;

	Для каждого СтрокаДанных Из Объект.ДанныеФайла Цикл 
		Ссылка = Справочники.уатРаботыПоРемонту.НайтиПоНаименованию(СтрокаДанных.Наименование);
		Если Ссылка.Пустая() Тогда
			Работа = Справочники.уатРаботыПоРемонту.СоздатьЭлемент();
			Работа.Артикул = СтрокаДанных.НомерПоКаталогу;
			Работа.Наименование = СтрокаДанных.Наименование;
			Работа.Записать();
			Ссылка = Работа.Ссылка;
		КонецЕсли;
		
		Для Каждого Элемент Из МассивСсылок Цикл
			МенеджерЗаписи = РегистрыСведений.уатНормочасыРаботПоРемонту.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Работа = Ссылка;
			МенеджерЗаписи.МодельТС = Элемент; 
			МенеджерЗаписи.КоличествоНЧ = СтрокаДанных.КоличествоНЧ;
			МенеджерЗаписи.Записать(Истина);
		КонецЦикла;
	КонецЦикла;		
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанные(Команда)
	ЗаписатьДанныеНаСервере();
	ПоказатьПредупреждение(,"Готово!!!!");
КонецПроцедуры

